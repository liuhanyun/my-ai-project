dependencies {
    implementation project(':domain')
    implementation 'org.springframework:spring-context'
    implementation 'com.mysql:mysql-connector-j'
    implementation 'org.mapstruct:mapstruct'
    implementation 'org.mybatis:mybatis'

    compileOnly 'org.projectlombok:lombok'
    compileOnly 'org.mybatis.generator:mybatis-generator-core'

    annotationProcessor 'org.mapstruct:mapstruct-processor'
    annotationProcessor 'org.projectlombok:lombok-mapstruct-binding'
    annotationProcessor 'org.projectlombok:lombok'
}

configurations {
    mybatisGenerator
}

configurations.mybatisGenerator.extendsFrom(configurations.implementation, configurations.compileOnly)

tasks.register('mybatisGenerate', JavaExec) {
    group = 'mybatis'
    description = 'Run MyBatis Generator'
    classpath = configurations.mybatisGenerator + files('src/main/resources')
    mainClass = 'org.mybatis.generator.api.ShellRunner'
    args '-configfile', 'src/main/resources/generatorConfig.xml', '-overwrite'

    doLast {
        fileTree('src/main/java/com/use/demo/infrastructure/dataobject')
            .matching { include '**/*DO.java' }
            .each { File file ->
                List<String> lines = file.readLines('UTF-8')
                List<String> out = []
                boolean skip = false
                int braceDepth = 0

                lines.each { line ->
                    if (!skip && line.contains('This method was generated by MyBatis Generator.')) {
                        while (!out.isEmpty() && out.last().trim() != '/**') {
                            out.remove(out.size() - 1)
                        }
                        if (!out.isEmpty() && out.last().trim() == '/**') {
                            out.remove(out.size() - 1)
                        }
                        skip = true
                        braceDepth = 0
                        return
                    }

                    if (skip) {
                        braceDepth += (line.findAll(/\{/).size())
                        braceDepth -= (line.findAll(/\}/).size())
                        if (braceDepth <= 0 && line.trim().endsWith('}')) {
                            skip = false
                        }
                        return
                    }

                    out << line
                }

                String content = out.join('\n')
                if (!content.contains('import lombok.Getter;')) {
                    content = content.replaceFirst(
                        /package [^;]+;\s*/,
                        { match -> match + "\nimport lombok.Getter;\nimport lombok.Setter;\n\n" }
                    )
                }
                if (!content.contains('@Getter')) {
                    content = content.replaceFirst(/public class /, "@Getter\n@Setter\npublic class ")
                }
                content = content.replaceAll(/@Setter\s+\n/, "@Setter\n")
                content = content.replaceAll(/\n{3,}/, "\n\n")
                file.setText(content.trim() + "\n", 'UTF-8')
            }
    }
}
